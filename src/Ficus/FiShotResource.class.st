"
Resource with sample Ficus metalevel objects.
"
Class {
	#name : #FiShotResource,
	#superclass : #Object,
	#category : #'Ficus-Tests'
}

{ #category : #resources }
FiShotResource >> classA [

	^ FiClass builder 
		theName: #A;
		layout: (self fixedLayoutWithInstanceVariables: #( #a1 #a2 ));
		methods: (FiSet withValues: { 
			self methodWith: 'sum  ^ a1 + a2'.
			self methodWith: 'sum42  ^ self sum + 42'. });
		new.
]

{ #category : #resources }
FiShotResource >> classB [

	^ FiClass builder 
		theName: #B;
		superclassName: #A;
		classPool: (FiSet withValues: #(BConstants));
		layout: (self fixedLayoutWithInstanceVariables: #(b1));
		methods: (FiSet withValues: { 
			self methodWith: 'sum  ^ super sum + b1'.
			self methodWith: 'variableInSuper  ^ a1'.
			self methodWith: 'defaultClass  ^ C'.
			});
		new.
]

{ #category : #resources }
FiShotResource >> classC [

	^ FiClass builder 
		theName: #C;
		layout: (self fixedLayoutWithInstanceVariables: #());
		methods: (FiSet withValues: { 
			self methodWith: 'sum  ^ 0'.
			self methodWith: 'undeclared  ^ undeclaredVar'.
			self methodWith: 'sumAt: argument  ^ argument sum' });
		new.
]

{ #category : #resources }
FiShotResource >> environmentWithClassesABC [

	^ FiSet withValues: { self packageXWithClassesABC }
]

{ #category : #resources }
FiShotResource >> fixedLayoutWithInstanceVariables: names [
	
	^ FiFixedLayout builder
		instanceVariables: (FiSet withValues: names);
		new
]

{ #category : #private }
FiShotResource >> methodWith: sourceCode [
	
	^ FiMethod builder
		protocol: #autogenerated;
		sourceCode: sourceCode;
		new

]

{ #category : #resources }
FiShotResource >> packageXWithClassesABC [
	
	^ FiPackage builder
		theName: #X;
		contents: (FiSet withValues: { self classA. self classB. self classC });
		new
]
