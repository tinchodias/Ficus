"
Resource with sample Ficus metalevel objects.
"
Class {
	#name : #FiShotResources,
	#superclass : #Object,
	#category : #'FicusRing-Tests'
}

{ #category : #resources }
FiShotResources >> classA [

	^ FiClass builder 
		theName: #A;
		layout: (self fixedLayoutWithInstanceVariables: #( #a1 #a2 ));
		methods: { 
			self methodWith: 'sum  ^ a1 + a2'.
			self methodWith: 'sum42  ^ self sum + 42'. } asFiSet;
		new.
]

{ #category : #resources }
FiShotResources >> classB [

	^ FiClass builder 
		theName: #B;
		superclassName: #A;
		classPool: #(BConstants) asFiSet;
		layout: (self fixedLayoutWithInstanceVariables: #(b1));
		methods: { 
			self methodWith: 'sum  ^ super sum + b1'.
			self methodWith: 'variableInSuper  ^ a1'.
			self methodWith: 'defaultClass  ^ C'.
			} asFiSet;
		new.
]

{ #category : #resources }
FiShotResources >> classC [

	^ FiClass builder 
		theName: #C;
		layout: (self fixedLayoutWithInstanceVariables: #());
		methods: { 
			self methodWith: 'sum  ^ 0'.
			self methodWith: 'undeclared  ^ undeclaredVar'.
			self methodWith: 'sumAt: argument  ^ argument sum' } asFiSet;
		new.
]

{ #category : #resources }
FiShotResources >> environmentWithClassesABC [

	^ { self packageXWithClassesABC } asFiSet
]

{ #category : #resources }
FiShotResources >> environmentWithClassesABCAndExtensionMethod [

	^ { self packageXWithClassesABC. self packageYWithExtensionMethod } asFiSet
]

{ #category : #resources }
FiShotResources >> extensionMethodsToX [

	^ FiExtensionMethods builder
		hostName: #A;
		methods: { 
			self methodWith: 'sum43  ^ self sum42 + 1'. } asFiSet;
		isMetaSide: false;
		new
]

{ #category : #resources }
FiShotResources >> fixedLayoutWithInstanceVariables: names [
	
	^ FiFixedLayout builder
		instanceVariables: names asFiSet;
		new
]

{ #category : #private }
FiShotResources >> methodWith: sourceCode [
	
	^ FiMethod builder
		protocol: #autogenerated;
		sourceCode: sourceCode;
		new

]

{ #category : #resources }
FiShotResources >> packageXWithClassesABC [
	
	^ FiPackage builder
		theName: #X;
		contents: { self classA. self classB. self classC } asFiSet;
		new
]

{ #category : #resources }
FiShotResources >> packageYWithExtensionMethod [
	
	^ FiPackage builder
		theName: #Y;
		contents: { self extensionMethodsToX } asFiSet;
		new
]
